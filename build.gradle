plugins {
	id 'java'
	id 'maven-publish'
	id 'com.enonic.defaults' version '2.0.1'
	id 'com.enonic.xp.app' version '2.0.0'
	id 'com.enonic.xp.run' version '2.0.0'
	id 'com.moowork.gulp' version '1.3.0'
	id 'com.moowork.node' version '1.3.1'
}


repositories {
	jcenter()
	xp.enonicRepo()
}


app {
	name = project.appName
	displayName = "Enonic Developer Site"
	vendorName = 'Enonic AS'
	vendorUrl = 'http://enonic.com'
	systemVersion = "${xpVersion}"
	instruction( "Private-Package", "*;-split-package:=merge-first;META-INF.jruby.home.*;" )
}


dependencies {
	include 'org.asciidoctor:asciidoctorj:1.6.2'
	compile "com.enonic.xp:core-api:${xpVersion}"
	compile "com.enonic.xp:portal-api:${xpVersion}"
	include "com.enonic.xp:lib-content:${xpVersion}"
	include "com.enonic.xp:lib-context:${xpVersion}"
	include "com.enonic.xp:lib-common:${xpVersion}"
	include "com.enonic.xp:lib-task:${xpVersion}"
	include "com.enonic.xp:lib-portal:${xpVersion}"
	include 'com.enonic.lib:lib-thymeleaf:2.0.0-B2'
	include 'org.jsoup:jsoup:1.10.2'
	include 'org.eclipse.jgit:org.eclipse.jgit:4.8.0.201706111038-r'
	include 'com.enonic.lib:lib-urlredirect:3.0.0-B1'
	testCompile 'junit:junit:4.10'
	testCompile 'org.mockito:mockito-core:1.+'
	include 'com.enonic.lib:lib-util:2.0.0-B2'
	include 'com.enonic.lib:lib-menu:3.0.0-B1'
	//include "com.enonic.lib:lib-markdown:1.0.0-B1"
	include "com.enonic.lib:lib-http-client:2.0.0-B1"
}




//──────────────────────────────────────────────────────────────────────────────
// Gradle node plugin
//──────────────────────────────────────────────────────────────────────────────
node {
	version '10.15.3'
	download true
}


//──────────────────────────────────────────────────────────────────────────────
// Gulp TODO This could/should be replaced with webpack
//──────────────────────────────────────────────────────────────────────────────
// makes sure on each build that gulp is installed
gulp_build.dependsOn 'installGulp'

// processes your package.json before running gulp build
gulp_build.dependsOn 'npmInstall'

// runs "gulp build" as part of your gradle build
jar.dependsOn gulp_build


//──────────────────────────────────────────────────────────────────────────────
// Webpack
//──────────────────────────────────────────────────────────────────────────────
sourceSets {
	main {
		java {
			srcDir 'src/main/java'
		}
		resources { // Paths relative to src/main/resources
			exclude '**/*.es' // Avoid having your es files copied into ./build
		}
	}
}

task webpack(type:NodeTask) {
	script = file('node_modules/webpack-cli/bin/cli.js')
	args = [
		'--color',
	]
}
webpack.dependsOn npmInstall
webpack.mustRunAfter npmInstall
assemble.dependsOn webpack
assemble.mustRunAfter webpack
